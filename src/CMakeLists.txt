PROJECT(LIGGGHTS C CXX)
CMAKE_MINIMUM_REQUIRED(VERSION 3.5.0)

SET(LIGGGHTS_MAJOR_VERSION 3)
SET(LIGGGHTS_MINOR_VERSION 8)
SET(LIGGGHTS_PATCH_VERSION 0)
SET(LIGGGHTS_VERSION ${LIGGGHTS_MAJOR_VERSION}.${LIGGGHTS_MINOR_VERSION}.${LIGGGHTS_PATCH_VERSION})

# TODO:
# https://www.cfdem.com/media/DEM/docu/Section_start.html#making-liggghts-r-public
# -DFFT_FFTW2
# -DFFT_FFTW3
# -DLAMMPS_MEMALIGN
# -DLAMMPS_XDR
# -DPACK_ARRAY
# -DPACK_POINTER
# -DPACK_MEMCPY

#=======================================
SET(CMAKE_MODULE_PATH "${CMAKE_CURRENT_SOURCE_DIR}/cMake")
INCLUDE(Version)
INCLUDE(Style)
INCLUDE(Model)

#=======================================
SET(ENABLED_OPTIONS "")
SET(DISABLED_OPTIONS "")

SET(ENABLED_MODELS "")

OPTION(DISABLE_ALL "Disable all features/models (OFF by default)" OFF)
OPTION(ENABLE_ALL  "Enable all features/models (OFF by default)"  OFF)

SET(DEFAULT_ON ON CACHE INTERNAL "Default ON value")
SET(DEFAULT_OFF OFF CACHE INTERNAL "Default OFF value")

IF(DISABLE_ALL)
  SET(DEFAULT_ON OFF)
ENDIF()

IF(ENABLE_ALL)
  SET(DEFAULT_OFF ON)
ENDIF()

OPTION(ENABLE_VTK    "Use dump_vtk" ${DEFAULT_ON})
OPTION(ENABLE_JPEG   "Use libjpeg"  ${DEFAULT_OFF})
OPTION(ENABLE_PNG    "Use libpng"   ${DEFAULT_OFF})
OPTION(ENABLE_FFMPEG "Use ffmpeg"   ${DEFAULT_OFF})
OPTION(ENABLE_GZIP   "Use gzip"     ${DEFAULT_OFF})
OPTION(ENABLE_MPI    "Use MPI"      ${DEFAULT_ON})

OPTION(ENABLE_SQ  "Use Superquadrics" ${DEFAULT_OFF})

OPTION(ENABLE_SMALLBIG   "Use of 4-byte (small) vs 8-byte (big) integers."        ON)
OPTION(ENABLE_BIGBIG     "Huge molecular systems with more than 2 billion atoms." OFF)
OPTION(ENABLE_SMALLSMALL "Machine does not support 64-bit integers."              OFF)

OPTION(ENABLE_LONGLONG_TO_LONG "System does not recognize “long long” data types." OFF)

# 9 normal models
OPTION(ENABLE_HERTZ "HERTZ Model" ${DEFAULT_ON})
OPTION(ENABLE_HOOKE "HOOKE Model" ${DEFAULT_OFF})
OPTION(ENABLE_HOOKE_HYSTERESIS "HOOKE_HYSTERESIS Model" ${DEFAULT_OFF})
OPTION(ENABLE_HOOKE_STIFFNESS "HOOKE_STIFFNESS Model" ${DEFAULT_OFF})
OPTION(ENABLE_EDINBURGH "EDINBURGH Model" ${DEFAULT_OFF})
OPTION(ENABLE_EDINBURGH_STIFFNESS "EDINBURGH_STIFFNESS Model" ${DEFAULT_OFF})
OPTION(ENABLE_HERTZ_STIFFNESS "HERTZ_STIFFNESS Model" ${DEFAULT_OFF})
OPTION(ENABLE_LUDING "LUDING Model" ${DEFAULT_OFF})
OPTION(ENABLE_THORNTON_NING "THORNTON_NING Model" ${DEFAULT_OFF})

# 4 tangential models
OPTION(ENABLE_TANGENTIAL_OFF "Tangential Model OFF" ${DEFAULT_OFF})
OPTION(ENABLE_TANGENTIAL_HISTORY "Tangential Model history" ${DEFAULT_ON})
OPTION(ENABLE_TANGENTIAL_NO_HISTORY "Tangential Model no_history" ${DEFAULT_ON})
OPTION(ENABLE_TANGENTIAL_LUDING "Tangential Model luding" ${DEFAULT_OFF})

# 5 cohesion models
OPTION(ENABLE_COHESION_OFF "Cohesion Model OFF" ${DEFAULT_OFF})
OPTION(ENABLE_COHESION_SJKR "Cohesion Model SJKR" ${DEFAULT_ON})
OPTION(ENABLE_COHESION_SJKR2 "Cohesion Model SJKR2" ${DEFAULT_ON})
OPTION(ENABLE_COHESION_WASHINO_CAPILLARY_VISCOUS "Cohesion Model washino" ${DEFAULT_OFF})
OPTION(ENABLE_COHESION_EASO_CAPILLARY_VISCOUS "Cohesion Model easo" ${DEFAULT_OFF})

# 6 rolling models
OPTION(ENABLE_ROLLING_OFF "Rolling Model off" ${DEFAULT_OFF})
OPTION(ENABLE_ROLLING_CDT "Rolling Model cdt" ${DEFAULT_OFF})
OPTION(ENABLE_ROLLING_EPSD "Rolling Model epsd" ${DEFAULT_OFF})
OPTION(ENABLE_ROLLING_EPSD2 "Rolling Model epsd2" ${DEFAULT_ON})
OPTION(ENABLE_ROLLING_EPSD3 "Rolling Model espd3" ${DEFAULT_OFF})
OPTION(ENABLE_ROLLING_LUDING "Rolling Model luding" ${DEFAULT_OFF})

# 3 surface models
OPTION(ENABLE_SURFACE_DEFAULT "Surface Model default" ${DEFAULT_ON})
OPTION(ENABLE_SURFACE_SUPERQUADRIC "Surface Model Superquadric" ${DEFAULT_OFF})
OPTION(ENABLE_SURFACE_MULTICONTACT "Surface Model Multicontact" ${DEFAULT_OFF})

#=======================================
IF("${CMAKE_CXX_COMPILER_ID}" STREQUAL "Clang")
  IF(CMAKE_CXX_COMPILER_VERSION VERSION_LESS 7.0.0)
    SET(CMAKE_CXX_STANDARD 14)
  ELSE()
    SET(CMAKE_CXX_STANDARD 17)
  ENDIF()
  SET(CMAKE_CXX_STANDARD_REQUIRED ON)  

  SET(OPT_CXX_FLAGS "")
  MESSAGE(STATUS "Compiler: clang")
ELSEIF(("${CMAKE_CXX_COMPILER_ID}" STREQUAL "GNU"))
  IF(CMAKE_CXX_COMPILER_VERSION VERSION_LESS 7.0.0)
    SET(CMAKE_CXX_STANDARD 14)
  ELSE()
    SET(CMAKE_CXX_STANDARD 17)
  ENDIF()
  SET(CMAKE_CXX_STANDARD_REQUIRED ON)

  SET(OPT_CXX_FLAGS "-O2 -ffast-math -funroll-loops -fstrict-aliasing -Wall -Wextra -Wno-uninitialized -Wno-literal-suffix -fPIC")
  MESSAGE(STATUS "Compiler: gcc")
ELSEIF("${CMAKE_CXX_COMPILER_ID}" STREQUAL "Intel")
	SET(OPT_CXX_FLAGS "-O3 -funroll-loops -fstrict-aliasing -W -Wno-uninitialized -march=native -xHost")
  MESSAGE(STATUS "Compiler: intel")
ELSEIF("${CMAKE_CXX_COMPILER_ID}" STREQUAL "MSVC")
  SET(OPT_CXX_FLAGS "/O2 /bigobj")

  ADD_DEFINITIONS(-D_CRT_SECURE_NO_WARNINGS -D_USE_MATH_DEFINES -DNOMINMAX)
  ADD_DEFINITIONS("/wd4101 /wd4244 /wd4267")

  MESSAGE(STATUS "Compiler: msvc")
ENDIF()

SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${OPT_CXX_FLAGS}")

IF(WIN32)
  INCLUDE_DIRECTORIES(WINDOWS/extra)

  FIND_PACKAGE(Cygwin REQUIRED)

  FOREACH(flag_var
    CMAKE_C_FLAGS_RELEASE CMAKE_C_FLAGS_MINSIZEREL CMAKE_C_FLAGS_RELWITHDEBINFO
    CMAKE_CXX_FLAGS_RELEASE CMAKE_CXX_FLAGS_MINSIZEREL CMAKE_CXX_FLAGS_RELWITHDEBINFO)
    SET(${flag_var} "${${flag_var}} /MP4")
  ENDFOREACH()
ENDIF()

#=======================================
# based on: https://github.com/schrummy14/LIGGGHTS_Flexible_Fibers/blob/master/src/WINDOWS/CMake_patch.zip

SCAN_STYLES()
GENERATE_STYLES()

#=======================================
# based on: https://github.com/schrummy14/LIGGGHTS_Flexible_Fibers/blob/master/src/WINDOWS/CMake_patch.zip

IF(ENABLE_HERTZ)
  ADD_CONTACT_MODEL("HERTZ" "" "" "" "" "")
  SET(ENABLED_MODELS "${ENABLED_MODELS} HERTZ")
ENDIF()
IF(ENABLE_HOOKE)
  ADD_CONTACT_MODEL("HOOKE" "" "" "" "" "")
  SET(ENABLED_MODELS "${ENABLED_MODELS} HOOKE")
ENDIF()
IF(ENABLE_HOOKE_HYSTERESIS)
  ADD_CONTACT_MODEL("HOOKE_HYSTERESIS" "" "" "" "" "")
  SET(ENABLED_MODELS "${ENABLED_MODELS} HOOKE_HYSTERESIS")
ENDIF()
IF(ENABLE_HOOKE_STIFFNESS)
  ADD_CONTACT_MODEL("HOOKE_STIFFNESS" "" "" "" "" "")
  SET(ENABLED_MODELS "${ENABLED_MODELS} HOOKE_STIFFNESS")
ENDIF()
IF(ENABLE_EDINBURGH)
  ADD_CONTACT_MODEL("EDINBURGH" "" "" "" "" "")
  SET(ENABLED_MODELS "${ENABLED_MODELS} EDINBURGH")
ENDIF()
IF(ENABLE_EDINBURGH_STIFFNESS)
  ADD_CONTACT_MODEL("EDINBURGH_STIFFNESS" "" "" "" "" "")
  SET(ENABLED_MODELS "${ENABLED_MODELS} EDINBURGH_STIFFNESS")
ENDIF()
IF(ENABLE_HERTZ_STIFFNESS)
  ADD_CONTACT_MODEL("HERTZ_STIFFNESS" "" "" "" "" "")
  SET(ENABLED_MODELS "${ENABLED_MODELS} HERTZ_STIFFNESS")
ENDIF()
IF(ENABLE_LUDING)
  ADD_CONTACT_MODEL("LUDING" "" "" "" "" "")
  SET(ENABLED_MODELS "${ENABLED_MODELS} LUDING")
ENDIF()
IF(ENABLE_THORNTON_NING)
  ADD_CONTACT_MODEL("THORNTON_NING" "" "" "" "" "")
  SET(ENABLED_MODELS "${ENABLED_MODELS} THORNTON_NING")
ENDIF()

# 4 tangential models
IF(ENABLE_TANGENTIAL_OFF)
  ADD_CONTACT_MODEL("" "TANGENTIAL_OFF" "" "" "" "")
  SET(ENABLED_MODELS "${ENABLED_MODELS} TANGENTIAL_OFF")
ENDIF()
IF(ENABLE_TANGENTIAL_HISTORY)
  ADD_CONTACT_MODEL("" "TANGENTIAL_HISTORY" "" "" "" "")
  SET(ENABLED_MODELS "${ENABLED_MODELS} TANGENTIAL_HISTORY")
ENDIF()
IF(ENABLE_TANGENTIAL_NO_HISTORY)
  ADD_CONTACT_MODEL("" "TANGENTIAL_NO_HISTORY" "" "" "" "")
  SET(ENABLED_MODELS "${ENABLED_MODELS} TANGENTIAL_NO_HISTORY")
ENDIF()
IF(ENABLE_TANGENTIAL_LUDING)
  ADD_CONTACT_MODEL("" "TANGENTIAL_LUDING" "" "" "" "")
  SET(ENABLED_MODELS "${ENABLED_MODELS} TANGENTIAL_LUDING")
ENDIF()

# 5 cohesion models
IF(ENABLE_COHESION_OFF)
  ADD_CONTACT_MODEL("" "" "COHESION_OFF" "" "")
  SET(ENABLED_MODELS "${ENABLED_MODELS} COHESION_OFF")
ENDIF()
IF(ENABLE_COHESION_SJKR)
  ADD_CONTACT_MODEL("" "" "COHESION_SJKR" "" "")
  SET(ENABLED_MODELS "${ENABLED_MODELS} COHESION_SJKR")
ENDIF()
IF(ENABLE_COHESION_SJKR2)
  ADD_CONTACT_MODEL("" "" "COHESION_SJKR2" "" "")
  SET(ENABLED_MODELS "${ENABLED_MODELS} COHESION_SJKR2")
ENDIF()
IF(ENABLE_COHESION_WASHINO_CAPILLARY_VISCOUS)
  ADD_CONTACT_MODEL("" "" "COHESION_WASHINO_CAPILLARY_VISCOUS" "" "")
  SET(ENABLED_MODELS "${ENABLED_MODELS} COHESION_WASHINO_CAPILLARY_VISCOUS")
ENDIF()
IF(ENABLE_COHESION_EASO_CAPILLARY_VISCOUS)
  ADD_CONTACT_MODEL("" "" "COHESION_EASO_CAPILLARY_VISCOUS" "" "")
  SET(ENABLED_MODELS "${ENABLED_MODELS} COHESION_EASO_CAPILLARY_VISCOUS")
ENDIF()

# 6 rolling models
IF(ENABLE_ROLLING_OFF)
  ADD_CONTACT_MODEL("" "" "" "ROLLING_OFF" "")
  SET(ENABLED_MODELS "${ENABLED_MODELS} ROLLING_OFF")
ENDIF()
IF(ENABLE_ROLLING_CDT)
  ADD_CONTACT_MODEL("" "" "" "ROLLING_CDT" "")
  SET(ENABLED_MODELS "${ENABLED_MODELS} ROLLING_CDT")
ENDIF()
IF(ENABLE_ROLLING_EPSD)
  ADD_CONTACT_MODEL("" "" "" "ROLLING_EPSD" "")
  SET(ENABLED_MODELS "${ENABLED_MODELS} ROLLING_EPSD")
ENDIF()
IF(ENABLE_ROLLING_EPSD2)
  ADD_CONTACT_MODEL("" "" "" "ROLLING_EPSD2" "")
  SET(ENABLED_MODELS "${ENABLED_MODELS} ROLLING_EPSD2")
ENDIF()
IF(ENABLE_ROLLING_EPSD3)
  ADD_CONTACT_MODEL("" "" "" "ROLLING_EPSD3" "")
  SET(ENABLED_MODELS "${ENABLED_MODELS} ROLLING_EPSD3")
ENDIF()
IF(ENABLE_ROLLING_LUDING)
  ADD_CONTACT_MODEL("" "" "" "ROLLING_LUDING" "")
  SET(ENABLED_MODELS "${ENABLED_MODELS} ROLLING_LUDING")
ENDIF()

# 3 surface models
IF(ENABLE_SURFACE_DEFAULT)
  ADD_CONTACT_MODEL("" "" "" "" "SURFACE_DEFAULT")
  SET(ENABLED_MODELS "${ENABLED_MODELS} SURFACE_DEFAULT")
ENDIF()
IF(ENABLE_SURFACE_SUPERQUADRIC)
  ADD_CONTACT_MODEL("" "" "" "" "SURFACE_SUPERQUADRIC")
  SET(ENABLED_MODELS "${ENABLED_MODELS} SURFACE_SUPERQUADRIC")
ENDIF()
IF(ENABLE_SURFACE_MULTICONTACT)
  ADD_CONTACT_MODEL("" "" "" "" "SURFACE_MULTICONTACT")
  SET(ENABLED_MODELS "${ENABLED_MODELS} SURFACE_MULTICONTACT")
ENDIF()

WRITE_WHITELIST()

#========================================
GET_SUBDIRS(dirList CMakeLists.txt)
FOREACH(dir ${dirList})
  ADD_SUBDIRECTORY(${dir})
ENDFOREACH()

#=======================================
FILE(GLOB SOURCES *.cpp)
FILE(GLOB HEADERS *.h)

ADD_LIBRARY(liggghts_obj OBJECT ${SOURCES})

ADD_LIBRARY(liggghts_static STATIC $<TARGET_OBJECTS:liggghts_obj>)
SET_TARGET_PROPERTIES(liggghts_static PROPERTIES OUTPUT_NAME liggghts)
SET_PROPERTY(TARGET liggghts_static PROPERTY PUBLIC_HEADER ${HEADERS})

ADD_LIBRARY(liggghts_shared SHARED $<TARGET_OBJECTS:liggghts_obj>)
SET_TARGET_PROPERTIES(liggghts_shared PROPERTIES OUTPUT_NAME liggghts)
SET_PROPERTY(TARGET liggghts_shared PROPERTY PUBLIC_HEADER ${HEADERS})

ADD_EXECUTABLE(liggghts_bin $<TARGET_OBJECTS:liggghts_obj>)
SET_TARGET_PROPERTIES(liggghts_bin PROPERTIES OUTPUT_NAME liggghts)

#=======================================
IF(ENABLE_MPI)
  FIND_PACKAGE(MPI)

  IF(MPI_FOUND)
    INCLUDE_DIRECTORIES(${MPI_INCLUDE_PATH})
    TARGET_LINK_LIBRARIES(liggghts_static ${MPI_LIBRARIES})
    TARGET_LINK_LIBRARIES(liggghts_shared ${MPI_LIBRARIES})
    TARGET_LINK_LIBRARIES(liggghts_bin ${MPI_LIBRARIES})

    IF(MPI_COMPILE_FLAGS)
      SET_TARGET_PROPERTIES(liggghts_static PROPERTIES COMPILE_FLAGS "${MPI_COMPILE_FLAGS}")
      SET_TARGET_PROPERTIES(liggghts_shared PROPERTIES COMPILE_FLAGS "${MPI_COMPILE_FLAGS}")
      SET_TARGET_PROPERTIES(liggghts_bin PROPERTIES COMPILE_FLAGS "${MPI_COMPILE_FLAGS}")
    ENDIF()

    IF(MPI_LINK_FLAGS)
      SET_TARGET_PROPERTIES(liggghts_static PROPERTIES LINK_FLAGS "${MPI_LINK_FLAGS}")
      SET_TARGET_PROPERTIES(liggghts_shared PROPERTIES LINK_FLAGS "${MPI_LINK_FLAGS}")
      SET_TARGET_PROPERTIES(liggghts_bin PROPERTIES LINK_FLAGS "${MPI_LINK_FLAGS}")
    ENDIF()

    SET(ENABLED_OPTIONS "${ENABLED_OPTIONS} MPI")
  ELSE()
    MESSAGE(FATAL_ERROR "MPI NOT found!")
  ENDIF()
ELSE()
  SET(DISABLED_OPTIONS "${DISABLED_OPTIONS} MPI")
  MESSAGE(STATUS "Using MPI stubs")
ENDIF()

#=======================================
IF(ENABLE_VTK)
  FIND_PACKAGE(VTK NO_MODULE)

  IF(VTK_FOUND)
    INCLUDE(${VTK_USE_FILE})
    ADD_DEFINITIONS(-DLAMMPS_VTK)
    TARGET_LINK_LIBRARIES(liggghts_static ${VTK_LIBRARIES})
    TARGET_LINK_LIBRARIES(liggghts_shared ${VTK_LIBRARIES})
    TARGET_LINK_LIBRARIES(liggghts_bin ${VTK_LIBRARIES})

    SET(ENABLED_OPTIONS "${ENABLED_OPTIONS} VTK")

    MESSAGE(STATUS "Found VTK: version " ${VTK_VERSION})
  ELSE()
    MESSAGE(FATAL_ERROR "VTK NOT found!")
  ENDIF()
ELSE()
  SET(DISABLED_OPTIONS "${DISABED_OPTIONS} VTK")
ENDIF()

#=======================================
IF(ENABLE_JPEG)
  FIND_PACKAGE(JPEG)

  IF(JPEG_FOUND)
    INCLUDE_DIRECTORIES(${JPEG_INCLUDE_DIR})
    ADD_DEFINITIONS(-DLAMMPS_JPEG)
    TARGET_LINK_LIBRARIES(liggghts_static jpeg)
    TARGET_LINK_LIBRARIES(liggghts_shared jpeg)
    TARGET_LINK_LIBRARIES(liggghts_bin jpeg)
    SET(ENABLED_OPTIONS "${ENABLED_OPTIONS} JPEG")
  ELSE()
    MESSAGE(FATAL_ERROR "JPEG NOT found!")
  ENDIF()
ELSE()
  SET(DISABLED_OPTIONS "${DISABLED_OPTIONS} JPEG")
ENDIF()

#=======================================
IF(ENABLE_PNG)
  FIND_PACKAGE(PNG)

  IF(PNG_FOUND)
    INCLUDE_DIRECTORIES(${PNG_INCLUDE_DIR})
    ADD_DEFINITIONS(-DLAMMPS_PNG)
    TARGET_LINK_LIBRARIES(liggghts_static ${PNG_LIBRARY})
    TARGET_LINK_LIBRARIES(liggghts_shared ${PNG_LIBRARY})
    TARGET_LINK_LIBRARIES(liggghts_bin ${PNG_LIBRARY})
    SET(ENABLED_OPTIONS "${ENABLED_OPTIONS} PNG")
  ELSE()
    MESSAGE(FATAL_ERROR "PNG NOT found!")
  ENDIF()
ELSE()
  SET(DISABLED_OPTIONS "${DISABLED_OPTIONS} PNG")
ENDIF()

#=======================================
IF(ENABLE_FFMPEG)
  FIND_PATH(AVCODEC_INCLUDE_DIR libavcodec/avcodec.h)
  FIND_LIBRARY(AVCODEC_LIBRARY avcodec)

  FIND_PATH(AVFORMAT_INCLUDE_DIR libavformat/avformat.h)
  FIND_LIBRARY(AVFORMAT_LIBRARY avformat)

  FIND_PATH(AVUTIL_INCLUDE_DIR libavutil/avutil.h)
  FIND_LIBRARY(AVUTIL_LIBRARY avutil)

  IF((NOT AVCODEC_INCLUDE_DIR-NOTFOUND)
    AND (NOT AVFORMAT_INCLUDE_DIR-NOTFOUND)
    AND (NOT AVUTIL_INCLUDE_DIR-NOTFOUND)
    AND (NOT AVCODEC_LIBRARY-NOTFOUND)
    AND (NOT AVFORMAT_LIBRARY-NOTFOUND)
    AND (NOT AVUTIL_LIBRARY-NOTFOUND)
  )
    INCLUDE_DIRECTORIES(${AVCODEC_INCLUDE_DIR} ${AVFORMAT_INCLUDE_DIR} ${AVUTIL_INCLUDE_DIR})
    ADD_DEFINITIONS(-DLAMMPS_FFMPEG)
    TARGET_LINK_LIBRARIES(liggghts_static ${AVCODEC_LIBRARY} ${AVFORMAT_LIBRARY} ${AVUTIL_LIBRARY})
    TARGET_LINK_LIBRARIES(liggghts_shared ${AVCODEC_LIBRARY} ${AVFORMAT_LIBRARY} ${AVUTIL_LIBRARY})
    TARGET_LINK_LIBRARIES(liggghts_bin ${AVCODEC_LIBRARY} ${AVFORMAT_LIBRARY} ${AVUTIL_LIBRARY})
    
    SET(ENABLED_OPTIONS "${ENABLED_OPTIONS} FFMPEG")
    
    MESSAGE(STATUS "FFMPEG found!")
  ELSE()
    MESSAGE(FATAL_ERROR "FFMPEG NOT found!")
  ENDIF()
ELSE()
  SET(DISABLED_OPTIONS "${DISABLED_OPTIONS} FFMPEG")
ENDIF()

#=======================================
IF(ENABLE_GZIP)
  FIND_PROGRAM(GZIP "gzip")

  IF(GZIP)
    ADD_DEFINITIONS(-DLAMMPS_GZIP)

    SET(ENABLED_OPTIONS "${ENABLED_OPTIONS} GZIP")

    MESSAGE(STATUS "GZIP found!")
  ELSE()
    MESSAGE(FATAL_ERROR "GZIP NOT found!")
  ENDIF()
ELSE()
  SET(DISABLED_OPTIONS "${DISABLED_OPTIONS} GZIP")
ENDIF()

#=======================================
IF(ENABLE_SQ)
  FIND_PACKAGE(Boost)

  IF(Boost_FOUND)
    INCLUDE_DIRECTORIES(${Boost_INCLUDE_DIR})
    ADD_DEFINITIONS(-DSUPERQUADRIC_ACTIVE_FLAG -DNONSPHERICAL_ACTIVE_FLAG)
    TARGET_LINK_LIBRARIES(liggghts_static ${Boost_LIBRARY})
    TARGET_LINK_LIBRARIES(liggghts_shared ${Boost_LIBRARY})
    TARGET_LINK_LIBRARIES(liggghts_bin ${Boost_LIBRARY})

    SET(ENABLED_OPTIONS "${ENABLED_OPTIONS} SQ")

    MESSAGE(STATUS "Using Superquadrics")
  ELSE()
    MESSAGE(FATAL_ERROR "Boost NOT found!")
  ENDIF()
ELSE()
  SET(DISABLED_OPTIONS "${DISABLED_OPTIONS} SQ")
ENDIF()

#=======================================
IF(ENABLE_SMALLBIG)
  IF(ENABLE_BIGBIG OR ENABLE_SMALLSMALL)
    MESSAGE(FATAL_ERROR "Use only one of these options: SMALLBIG, BIGBIG, SMALLSMALL")
  ENDIF()

  ADD_DEFINITIONS(-DLAMMPS_SMALLBIG)
  SET(ENABLED_OPTIONS "${ENABLED_OPTIONS} SMALLBIG")
ELSEIF(ENABLE_BIGBIG)
  IF(ENABLE_SMALLBIG OR ENABLE_SMALLSMALL)
    MESSAGE(FATAL_ERROR "Use only one of these options: SMALLBIG, BIGBIG, SMALLSMALL")
  ENDIF()

  ADD_DEFINITIONS(-DLAMMPS_BIGBIG)
  SET(ENABLED_OPTIONS "${ENABLED_OPTIONS} BIGBIG")
ELSEIF(ENABLE_SMALLSMALL)
  IF(ENABLE_BIGBIG OR ENABLE_SMALLBIG)
    MESSAGE(FATAL_ERROR "Use only one of these options: SMALLBIG, BIGBIG, SMALLSMALL")
  ENDIF()
  
  ADD_DEFINITIONS(-DLAMMPS_SMALLSMALL)
  SET(ENABLED_OPTIONS "${ENABLED_OPTIONS} SMALLSMALL")
ELSE()
  ADD_DEFINITIONS(-DLAMMPS_SMALLBIG)
  SET(ENABLED_OPTIONS "${ENABLED_OPTIONS} SMALLBIG")
ENDIF()

IF(ENABLE_LONGLONG_TO_LONG)
  ADD_DEFINITIONS(-DLAMMPS_LONGLONG_TO_LONG)
  SET(ENABLED_OPTIONS "${ENABLED_OPTIONS} LONGLONG_TO_LONG")
ENDIF()

#=======================================
GENERATE_VERSION_H()

#=======================================
MESSAGE(STATUS "=======================================")
MESSAGE(STATUS "liggghts ${LIGGGHTS_VERSION}")
MESSAGE(STATUS "Enabled options:${ENABLED_OPTIONS}")
MESSAGE(STATUS "Disabled options:${DISABLED_OPTIONS}")
MESSAGE(STATUS "Enabled models:${ENABLED_MODELS}")
MESSAGE(STATUS "=======================================")

#=======================================
INSTALL(TARGETS liggghts_bin liggghts_static liggghts_shared
  RUNTIME DESTINATION bin
  LIBRARY DESTINATION lib
  ARCHIVE DESTINATION lib
  PUBLIC_HEADER DESTINATION include)
IF(WIN32)
  INSTALL(DIRECTORY WINDOWS DESTINATION include)
ENDIF()